import org.ccil.cowan.tagsoup.Parser
import org.gradle.api.tasks.testing.logging.TestExceptionFormat

/*
 * Copyright Â© 2020 NHSX. All rights reserved.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply from: "$rootDir/ktlint.gradle"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "uk.nhs.nhsx.colocate"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 20
        versionName "0.1 (build #${versionCode})"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }

    buildTypes {
        staging {
            debuggable true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging.exceptionFormat = TestExceptionFormat.FULL
            testLogging.showStandardStreams = true
        }

        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

task("outputInstrumentationErrors") {
    doFirst {
        def reportsDir = "$buildDir/reports/androidTests/connected"
        def htmlFiles = fileTree(dir: reportsDir, include: "*.html").getFiles()
        def slurper = new XmlSlurper(new Parser())

        htmlFiles
            .collectMany { file ->
                def html = slurper.parse(file)
                html.'**'.findAll { node -> node.name() == 'pre' }*.text()
            }
            .each { stacktrace ->
                System.err.println("\n==============================\n$stacktrace==============================\n")
            }
    }
}

afterEvaluate {
    tasks["connectedDebugAndroidTest"].finalizedBy("outputInstrumentationErrors")
}

def room_version = "2.2.5"
def timber_version = "4.7.1"
def lifecycle_version = "2.2.0"
def rx_android_ble_version = "1.11.1"

dependencies {
    implementation project(":api")
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.firebase:firebase-messaging:20.1.3'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.fragment:fragment-ktx:1.2.3"

    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"

    kapt 'com.google.dagger:dagger-compiler:2.27'
    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.27'
    implementation 'com.google.dagger:dagger:2.27'

    implementation "com.polidea.rxandroidble2:rxandroidble:$rx_android_ble_version"

    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.9.2"
    implementation "com.squareup.moshi:moshi:1.9.2"
    implementation "com.squareup.okio:okio:2.5.0"

    implementation "com.jakewharton.timber:timber:$timber_version"

    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    implementation 'com.google.firebase:firebase-analytics:17.3.0'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta03'

    implementation 'com.jakewharton:process-phoenix:2.0.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    // have to use 3.x.x version since Firebase crashlytics doesn't work with 4.x.x
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.14.7'
    androidTestImplementation 'org.assertj:assertj-core:3.15.0'
    androidTestImplementation "com.polidea.rxandroidble2:mockclient:$rx_android_ble_version"
    androidTestImplementation 'org.awaitility:awaitility-kotlin:3.1.6'

    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.5'
    testImplementation 'net.lachlanmckee:timber-junit-rule:1.0.1'
    testImplementation 'org.assertj:assertj-core:3.15.0'
    testImplementation 'org.awaitility:awaitility-kotlin:3.1.6'
    testImplementation 'io.mockk:mockk:1.9.3'
    testRuntimeOnly 'net.bytebuddy:byte-buddy:1.10.8' // https://github.com/mockk/mockk/issues/397

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestUtil 'androidx.test:orchestrator:1.2.0'
}
