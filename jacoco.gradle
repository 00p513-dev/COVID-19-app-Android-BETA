apply plugin: 'jacoco'

jacoco { toolVersion = "0.8.5" }

tasks.withType(Test) { jacoco.includeNoLocationClasses = true }

def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        '**/com/example/databinding/*',
        '**/com/example/generated/callback/*',
        '**/android/databinding/*',
        '**/androidx/databinding/*',
        '**/di/module/*',
        '**/*MapperImpl*.*',
        '**/*$ViewInjector*.*',
        '**/*$ViewBinder*.*',
        '**/BuildConfig.*',
        '**/*Component*.*',
        '**/*BR*.*',
        '**/Manifest*.*',
        '**/*$Lambda$*.*',
        '**/*Companion*.*',
        '**/*Module.*',
        '**/*Dagger*.*',
        '**/*MembersInjector*.*',
        '**/*_Factory*.*',
        '**/*_Provide*Factory*.*',
        '**/*Extensions*.*',
        '**/*$Result.*', /* filtering `sealed` and `data` classes */
        '**/*$Result$*.*'/* filtering `sealed` and `data` classes */
]

subprojects {
    apply plugin: 'jacoco'
}

project.evaluationDependsOnChildren()

project.afterEvaluate {
    tasks.create(
            name: "jacocoRootReport",
            type: JacocoReport,
    ) {
        def classDirs = []
        def sourceDirs = []
        def executions = []

        [
                project("api"),
                project("app")
        ].each { prj ->
            dependsOn(prj.testDebugUnitTest)
            classDirs += fileTree(dir: "${prj.buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
            sourceDirs += files("${prj.projectDir}/src/main/java")
            executions += "${prj.buildDir}/jacoco/testDebugUnitTest.exec"
        }

        classDirectories.setFrom(files(classDirs))
        additionalSourceDirs.setFrom(*sourceDirs)
        sourceDirectories.setFrom(*sourceDirs)

        executionData(*executions)

        reports {
            html.enabled = true
        }
    }

}

